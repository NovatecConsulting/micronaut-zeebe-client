plugins {
    id("com.github.johnrengelman.shadow")
    id("io.micronaut.application")
}

group = "info.novatec"

micronaut {
    runtime("netty")
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("micronaut.zeebe.client.example.*")
    }
}

dependencies {
    implementation(project(":micronaut-zeebe-client-feature"))

    implementation("javax.annotation:javax.annotation-api")
    runtimeOnly("ch.qos.logback:logback-classic")

    testImplementation("ch.qos.logback:logback-classic")
    testImplementation("io.zeebe:zeebe-test-container:3.3.0")
    testImplementation("org.testcontainers:junit-jupiter:1.17.3")
    testImplementation("io.camunda:zeebe-process-test:1.3.3")
}

application {
    mainClass.set("info.novatec.micronaut.zeebe.client.example.ZeebeApplication")
}

java {
    sourceCompatibility = JavaVersion.toVersion("1.8")
    targetCompatibility = JavaVersion.toVersion("1.8")
}

sourceSets {
    test {
        java {
            if (JavaVersion.current() == JavaVersion.VERSION_1_8) {
                exclude '**/ProcessTest.*'
            }
        }
    }
}

graalvmNative {
    binaries {
        main {
            buildArgs.add("-H:-UseServiceLoaderFeature")
        }
    }
}

graalvmNative.toolchainDetection = false
