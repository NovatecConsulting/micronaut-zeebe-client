plugins {
    id("org.jetbrains.kotlin.jvm")
    id("org.jetbrains.kotlin.kapt")
    id("org.jetbrains.kotlin.plugin.allopen")
    id ("io.micronaut.library")
    id("maven-publish")
    id("signing")
}

group "info.novatec"

micronaut {
    testRuntime("junit5")
    processing {
        incremental(true)
        annotations("micronaut.zeebe.client.feature.*")
    }
}

dependencies {
    implementation("io.micronaut:micronaut-runtime")

    api("io.camunda:zeebe-client-java:$zeebeVersion")

    runtimeOnly("ch.qos.logback:logback-classic")
    runtimeOnly("org.yaml:snakeyaml")

    kaptTest platform("io.micronaut.platform:micronaut-platform:$micronautVersion")
    kapt("io.micronaut:micronaut-inject-java:$micronautVersion")
    testImplementation("io.micronaut.test:micronaut-test-junit5")
    testImplementation("org.assertj:assertj-core")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
}

java {
    sourceCompatibility = JavaVersion.toVersion(javaLanguageVersion)
    targetCompatibility = JavaVersion.toVersion(javaLanguageVersion)
}

java {
    withJavadocJar()
    withSourcesJar()
}
tasks {
    compileTestKotlin {
        kotlinOptions {
            jvmTarget = javaLanguageVersion
        }
    }
}

// based on https://docs.gradle.org/current/userguide/publishing_maven.html#sec:modifying_the_generated_pom and https://central.sonatype.org/pages/requirements.html#sufficient-metadata
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'micronaut-zeebe-client-feature'
                description = 'Zeebe Client for Micronaut'
                url = 'https://github.com/camunda-community-hub/micronaut-zeebe-client'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'The micronaut-camunda Team'
                        organization = 'Camunda Community'
                        organizationUrl = 'https://github.com/camunda-community-hub'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/camunda-community-hub/micronaut-zeebe-client.git'
                    developerConnection = 'scm:git:https://github.com/camunda-community-hub/micronaut-zeebe-client.git'
                    url = 'https://github.com/camunda-community-hub/micronaut-zeebe-client'
                }
            }
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots/' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username 'micronaut-camunda'
                password "$System.env.OSSRH_PWD"
            }
        }
    }
}

// see https://docs.gradle.org/current/userguide/signing_plugin.html#sec:specifying_what_to_sign and https://central.sonatype.org/pages/working-with-pgp-signatures.html#generating-a-key-pair
signing {
    sign publishing.publications.mavenJava
}
